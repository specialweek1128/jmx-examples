plugins {
  id 'org.springframework.boot' version '2.2.5.RELEASE'
  id 'io.spring.dependency-management' version '1.0.9.RELEASE'
  id 'java'
  id 'war'
  id 'eclipse'
}

group = 'examples'
version = '1.0.0'

def javaVersion = JavaVersion.VERSION_13
sourceCompatibility = javaVersion
targetCompatibility = javaVersion

def defaultEncoding = 'UTF-8'
[compileJava, compileTestJava]*.options*.encoding = defaultEncoding

repositories {
  mavenCentral()
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
  testImplementation('org.springframework.boot:spring-boot-starter-test') {
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }

  implementation 'commons-cli:commons-cli:1.4'
}

test {
  useJUnitPlatform()
}

def excludeFile = '**/.gitkeep'
sourceSets {
  main {
    java {
      exclude excludeFile
    }
  }
  test {
    java {
      exclude excludeFile
    }
  }
}

processResources {
  exclude excludeFile
}

processTestResources {
  exclude excludeFile
}

springBoot {
    mainClassName = 'examples.jmx.JmxExamplesApplication'
}
